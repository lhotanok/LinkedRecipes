# Select all recipes that contain chicken and mozzarella.
# Exclude recipes with broccoli ingredient.

PREFIX schema: <http://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX ex: <http://example.org/ns#>

SELECT ?recipe (GROUP_CONCAT(?ingredientLabel;separator=" | ") AS ?ingredientLabels)
WHERE {
    ?recipe schema:recipeIngredient ?ingredient .
    {
        ?ingredient rdfs:label ?ingredientLabel .
    } UNION {
        ?ingredient ex:ingredient ?ingredientEntity .
        ?ingredientEntity skos:prefLabel ?ingredientLabel .
    }

    BIND(IF(CONTAINS(LCASE(STR(?ingredientLabel)), "chicken"), true, false) as ?containsChicken) .
    BIND(IF(CONTAINS(LCASE(STR(?ingredientLabel)), "mozzarella"), true, false) as ?containsMozzarella) .
  	BIND(IF(CONTAINS(LCASE(STR(?ingredientLabel)), "broccoli"), true, false) as ?containsBroccoli) .
    
    # Filter recipes that include queried ingredients (broccoli recipes will be excluded afterwards).
    FILTER(?containsChicken = true || ?containsMozzarella = true || ?containsBroccoli = true)
}
GROUP BY ?recipe
HAVING (CONTAINS(LCASE(STR(?ingredientLabels)), "chicken")
  && CONTAINS(LCASE(STR(?ingredientLabels)), "mozzarella")
  && !CONTAINS(LCASE(STR(?ingredientLabels)), "broccoli"))
